plugins {
  id 'java'

  alias(libs.plugins.springBoot)
  alias(libs.plugins.dependencyManagement)
  alias(libs.plugins.hibernateOrm)
  alias(libs.plugins.graalvmNative)
  alias(libs.plugins.errorProne)
  alias(libs.plugins.nullaway)
  alias(libs.plugins.spotless)

  id 'checkstyle'
  id 'jacoco'
}

group = 'com.quezap'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
  sourceCompatibility = libs.versions.javaLts.get() as String
  targetCompatibility = libs.versions.javaLts.get() as String

  toolchain {
    languageVersion = JavaLanguageVersion.of(libs.versions.javaLts.get() as String)
  }
}

configurations {
  mockitoAgent {
    transitive = false
  }

  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()
}

configurations.all {
  resolutionStrategy {
    force 'com.google.guava:guava:33.5.0-jre'
  }
}

dependencies {
// Error Prone / NullAway
  errorprone libs.error.prone.core
  errorprone libs.nullaway

  // JSpecify (référence via le catalogue)
  compileOnly libs.jspecify
  testCompileOnly libs.jspecify

  // Starters Spring Boot (versions gérées par le BOM)
  implementation libs.spring.boot.starter.actuator
  implementation libs.spring.boot.starter.data.jpa
  implementation libs.spring.boot.starter.security
  implementation libs.spring.boot.starter.validation
  implementation libs.spring.boot.starter.web
  implementation libs.spring.shell.starter

  // Autres dépendances
  implementation libs.tika.core
  implementation libs.tika.parser

  // S3 (plateforme et modules)
  implementation platform(libs.aws.bom)
  implementation libs.s3
  implementation libs.auth

  // Autres
  implementation libs.serial.int.caster
  implementation libs.profanity.filter
  implementation libs.bouncy.castle

  developmentOnly libs.spring.boot.devtools

  runtimeOnly libs.h2
  runtimeOnly libs.postgresql

  annotationProcessor libs.spring.boot.config.processor
  testImplementation libs.spring.boot.starter.test

  testImplementation libs.spring.security.test
  testRuntimeOnly libs.junit.platform.launcher

  mockitoAgent(libs.mockito.core)
}

// hibernate {
// 	enhancement {
// 		enableAssociationManagement = true
// 	}
// }

// --- Configuration Error Prone / NullAway (Conditionnelle) ---
def enableNullAway = project.hasProperty('nullaway') && project.property('nullaway') == 'true'

tasks.withType(JavaCompile).configureEach {
  // Désactiver Error Prone complètement pour les tâches AOT (fichiers générés)
  def isAotTask = name in ['compileAotJava', 'compileAotTestJava']

  options.errorprone {
    if (isAotTask) {
      // Désactiver complètement Error Prone pour les tâches AOT
      enabled = false
    } else if (enableNullAway) {
      // Activer NullAway pour les tâches normales
      check('NullAway', net.ltgt.gradle.errorprone.CheckSeverity.ERROR)
      option('NullAway:AnnotatedPackages', 'com.quezap')
      option('NullAway:JSpecifyMode', 'true')
      option('TreatGeneratedAsUnannotated', 'true')
    } else {
      // Si NullAway n'est pas activé, le désactiver
      option('NullAway:AnnotatedPackages', 'com.quezap')
      option('NullAway:JSpecifyMode', 'true')
      check('NullAway', net.ltgt.gradle.errorprone.CheckSeverity.OFF)
    }
  }

  if (enableNullAway && !isAotTask) {
    options.compilerArgs << '-Werror'
  }

  options.compilerArgs << "-Xlint:deprecation"
}

// --- Configuration Spotless ---
spotless {
  // * Java
  java {
    // * Utiliser Google Java Format
    googleJavaFormat(libs.versions.googleJavaFormat.get())
    removeUnusedImports()
    importOrder(
        'java',
        'javax',
        'org.springframework',
        'com.quezap',
        '#',
        '',
        )
    formatAnnotations()
    target('**/*.java')
    targetExclude('**/build/**', '**/generated/**')
  }

  // * Scripts Gradle Groovy
  groovyGradle {

    target('**/*.gradle')
    targetExclude('**/build/**')

    leadingTabsToSpaces(2)
  }
}

// * Intègre Spotless dans 'check'
tasks.named('check') {
  dependsOn('spotlessCheck', 'checkstyleMain', 'checkstyleTest')
}

// Configuration Checkstyle
checkstyle {
  toolVersion = libs.versions.checkstyle.get()

  configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")

  configProperties = [
    'org.checkstyle.google.suppressionfilter.config': file("${rootDir}/config/checkstyle/checkstyle-suppressions.xml"),
  ]
}

// Configuration des tâches de compilation/vérification (pour Main et Test)
tasks.withType(Checkstyle).configureEach {
  enabled = fileTree('src/main/java').files.size() > 0

  reports {
    // Exiger le rapport HTML (facile à lire)
    html.required = true
    // Désactiver le rapport XML (facultatif)
    xml.required = false
  }
}

// Désactiver Checkstyle pour les fichiers générés par AOT
tasks.named('checkstyleAot') {
  enabled = false
}

// Désactiver Checkstyle pour les fichiers générés par AOT
tasks.named('checkstyleAotTest') {
  enabled = false
}

// --- Tâches diverses ---

graalvmNative {
  metadataRepository {
    enabled = true
  }
  agent {
    defaultMode = "conditional"
    modes {
      conditional {
        userCodeFilterPath = "user-code-filter.json"
      }
    }
    metadataCopy {
      mergeWithExisting = true
      inputTaskNames.add("test")
      outputDirectories.add("src/main/resources/META-INF/native-image")
    }
  }
  binaries.configureEach {
    buildArgs.add('-march=compatibility')
  }
}

// --- Configuration JaCoCo (Couverture de Code) ---
jacoco {
  toolVersion = libs.versions.jacoco.get()
}

tasks.named('jacocoTestReport') {
  dependsOn tasks.named('test')

  reports {
    html.required = true
    html.outputLocation = layout.buildDirectory.dir('reports/jacoco/test/html')

    xml.required = true
    xml.outputLocation = layout.buildDirectory.file('reports/jacoco/test/jacocoTestReport.xml')

    csv.required = false
  }
}

// --- Configuration de l'exécution des tests (pour JaCoCo) ---
tasks.named('test', Test) {
  useJUnitPlatform()

  jacoco {
    destinationFile = layout.buildDirectory.file('jacoco/test.exec').get().asFile
    classDumpDir = layout.buildDirectory.dir('jacoco/classpathdumps').get().asFile
  }

  jvmArgs "-javaagent:${configurations.mockitoAgent.asPath}"

  jvmArgs += "-Xshare:off"
}

project.afterEvaluate {
  tasks.named('jacocoTestReport').configure {
    classDirectories.setFrom(files(classDirectories.files.collect {
      fileTree(dir: it, exclude: [
        // Exclure les classes Spring Boot standards
        '**/com/quezap/*Application.class',
        // Exclure les classes de configuration/interfaces DTO sans logique
        '**/*Configuration.class',
        '**/*Dto.class',
        '**/*MapperImpl.class',
        '**/*Constants.class',
        // Exclure les classes gérées par l'ORM si elles n'ont que des getters/setters
        '**/domain/entities/**',
      ])
    }))
  }
}

bootRun {
  standardInput = System.in
}
