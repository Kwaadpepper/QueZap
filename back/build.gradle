plugins {
  alias(libs.plugins.springBoot) apply false
  alias(libs.plugins.dependencyManagement) apply false
  alias(libs.plugins.hibernateOrm) apply false
  alias(libs.plugins.graalvmNative) apply false
  alias(libs.plugins.errorProne) apply false
  alias(libs.plugins.nullaway) apply false
  alias(libs.plugins.spotless)

  id 'checkstyle'
  id 'jacoco'
}

group = 'com.quezap'
version = '0.0.1-SNAPSHOT'
description = 'Quizz application backend'

def applicationProjects = [':quezap-api', ':quezap-cli']

subprojects {
  var projectIsAnApplication = applicationProjects.contains(project.path)
  if (projectIsAnApplication) {
    apply plugin: 'java'
  } else {
    apply plugin: 'java-library'
    apply plugin: libs.plugins.dependencyManagement.get().pluginId
  }
  apply plugin: libs.plugins.errorProne.get().pluginId
  apply plugin: libs.plugins.nullaway.get().pluginId
  apply plugin: 'jacoco'

  repositories { mavenCentral() }

  java {
    sourceCompatibility = libs.versions.javaLts.get() as String
    targetCompatibility = libs.versions.javaLts.get() as String
    toolchain { languageVersion = JavaLanguageVersion.of(libs.versions.javaLts.get() as String) }
  }

  configurations {
    mockitoAgent { transitive = false }
    compileOnly { extendsFrom annotationProcessor }
  }

  // BOM Spring Boot only to libraries (apps inherit via Spring Boot plugin)
  if (!projectIsAnApplication) {
    dependencyManagement {
      imports { mavenBom(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES) }
    }
  }

  // Override Guava version to avoid conflicts
  configurations.all { resolutionStrategy { force 'com.google.guava:guava:33.5.0-jre' } }

  // All modules dependencies
  dependencies {
    errorprone libs.error.prone.core
    errorprone libs.nullaway

    compileOnly libs.jspecify
    testCompileOnly libs.jspecify

    testImplementation libs.spring.boot.starter.test
    testRuntimeOnly libs.junit.platform.launcher

    mockitoAgent(libs.mockito.core)
  }

  // --- Error Prone / NullAway ---
  def enableNullAway = project.hasProperty('nullaway') && project.property('nullaway') == 'true'
  tasks.withType(JavaCompile).configureEach {
    def isAotTask = name.toLowerCase().contains('aot')
    options.errorprone {
      if (isAotTask) {
        enabled = false
      } else if (enableNullAway) {
        check('NullAway', net.ltgt.gradle.errorprone.CheckSeverity.ERROR)
        option('NullAway:AnnotatedPackages', 'com.quezap')
        option('NullAway:JSpecifyMode', 'true')
        option('TreatGeneratedAsUnannotated', 'true')
      } else {
        option('NullAway:AnnotatedPackages', 'com.quezap')
        option('NullAway:JSpecifyMode', 'true')
        check('NullAway', net.ltgt.gradle.errorprone.CheckSeverity.OFF)
      }
    }
    if (enableNullAway && !isAotTask) { options.compilerArgs << '-Werror' }
    options.compilerArgs << '-Xlint:deprecation'
  }

  // --- Checkstyle ---
  apply plugin: 'checkstyle'
  checkstyle {
    toolVersion = libs.versions.checkstyle.get()
    configFile = file("${rootDir}/checkstyle/checkstyle.xml")
    configProperties = [
      'org.checkstyle.google.suppressionfilter.config': file("${rootDir}/checkstyle/checkstyle-suppressions.xml"),
    ]
  }
  tasks.withType(Checkstyle).configureEach {
    reports { html.required = true; xml.required = false }
    // Ignore generated sources and anything under build/
    exclude('**/build/**')
    exclude('**/generated/**')
    exclude('**/generated-sources/**')
    exclude('**/generated/aotSources/**')
    // Do not run Checkstyle on AOT-generated source set
    if (name.toLowerCase().contains('aot')) { enabled = false }
  }

  // --- Tests JUnit Platform + JaCoCo ---
  tasks.withType(Test).configureEach {
    useJUnitPlatform()
    jacoco {
      destinationFile = layout.buildDirectory.file("jacoco/${name}.exec").get().asFile
      classDumpDir = layout.buildDirectory.dir('jacoco/classpathdumps').get().asFile
    }
    jvmArgs "-javaagent:${configurations.mockitoAgent.asPath}"
    jvmArgs += '-Xshare:off'
  }
}

// Dependencies specific to modules
project(":modules:shared") {
  dependencies {
    compileOnly(libs.jspecify)

    testImplementation(libs.spring.boot.starter.test)
    testRuntimeOnly(libs.junit.platform.launcher)
  }
}

project(":modules:domain") {
  dependencies {
    api(project(":modules:shared"))
    compileOnly(libs.jspecify)

    testImplementation(libs.spring.boot.starter.test)
    testRuntimeOnly(libs.junit.platform.launcher)
  }
}

project(":modules:application") {
  dependencies {
    api(project(":modules:domain"))
    api(project(":modules:shared"))

    implementation("org.springframework.boot:spring-boot-starter")
    implementation(libs.spring.boot.starter.web)
    implementation("org.springframework.boot:spring-boot-starter-aop")
    implementation("org.springframework:spring-tx")
    implementation(libs.spring.boot.starter.validation)
    implementation(libs.spring.boot.starter.security)
    implementation("com.fasterxml.jackson.core:jackson-databind")
    implementation(libs.tika.core)
    implementation(libs.tika.parser)

    testImplementation(libs.spring.boot.starter.test)
    testRuntimeOnly(libs.junit.platform.launcher)
  }
}

project(":modules:infrastructure") {
  dependencies {
    api(project(":modules:application"))
    implementation(project(":modules:domain"))

    implementation("org.springframework.boot:spring-boot-starter")
    implementation("org.springframework:spring-tx")
    implementation("org.springframework.security:spring-security-crypto")
    implementation(libs.tika.core)
    implementation(libs.tika.parser)
    implementation(platform(libs.aws.bom))
    implementation(libs.s3)
    implementation(libs.auth)
    implementation(libs.bouncy.castle)
    implementation(libs.jjwt.api)
    implementation(libs.jjwt.impl)
    implementation(libs.jjwt.jackson)
    implementation(libs.profanity.filter)
    implementation(libs.serial.int.caster)

    testImplementation(libs.spring.boot.starter.test)
    testRuntimeOnly(libs.junit.platform.launcher)
  }
}

project(":quezap-api") {
  apply plugin: libs.plugins.springBoot.get().pluginId
  apply plugin: libs.plugins.dependencyManagement.get().pluginId
  apply plugin: libs.plugins.graalvmNative.get().pluginId

  dependencyManagement {
    imports { mavenBom(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES) }
  }

  dependencies {
    implementation(project(":modules:application"))
    implementation(project(":modules:domain"))
    implementation(project(":modules:infrastructure"))

    implementation(libs.spring.boot.starter.web)
    implementation(libs.spring.boot.starter.validation)
    implementation(libs.spring.boot.starter.actuator)
    implementation(libs.spring.boot.starter.data.jpa)
    implementation(libs.spring.boot.starter.security)
    developmentOnly(libs.spring.boot.devtools)
    runtimeOnly(libs.h2)
    runtimeOnly(libs.postgresql)
    annotationProcessor(libs.spring.boot.config.processor)

    testImplementation(libs.spring.boot.starter.test)
    testImplementation(libs.spring.security.test)
    testRuntimeOnly(libs.junit.platform.launcher)
  }

  graalvmNative {
    metadataRepository { enabled = true }
    agent {
      defaultMode = "conditional"
      modes { conditional { userCodeFilterPath = "${project.rootDir}/user-code-filter.json" } }
      metadataCopy {
        mergeWithExisting = true
        inputTaskNames.add("test")
        outputDirectories.add("src/main/resources/META-INF/native-image")
      }
    }
    binaries {
      named("main") {
        imageName.set("quezap-api")
        sharedLibrary.set(false)
        buildArgs.addAll('-H:+ReportExceptionStackTraces', '--no-fallback', '-march=compatibility')
      }
    }
  }
}

project(":quezap-cli") {
  apply plugin: libs.plugins.springBoot.get().pluginId
  apply plugin: libs.plugins.dependencyManagement.get().pluginId
  apply plugin: libs.plugins.graalvmNative.get().pluginId

  dependencyManagement {
    imports { mavenBom(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES) }
  }

  dependencies {
    implementation(project(":modules:application"))
    implementation(project(":modules:infrastructure"))
    implementation("org.springframework.boot:spring-boot-starter-aop")
    implementation(libs.spring.boot.starter.data.jpa)
    runtimeOnly(libs.h2)
    implementation(libs.spring.shell.starter)
    annotationProcessor(libs.spring.boot.config.processor)

    testImplementation(libs.spring.boot.starter.test)
    testRuntimeOnly(libs.junit.platform.launcher)
  }

  // Allow bootRun to accept input from console
  tasks.named('bootRun') { configure { standardInput = System.in } }

  graalvmNative {
    metadataRepository { enabled = true }
    binaries {
      named("main") {
        imageName.set("quezap-cli")
        sharedLibrary.set(false)
        buildArgs.addAll('-H:+ReportExceptionStackTraces', '--no-fallback', '-march=compatibility')
      }
    }
  }
}

// --- Spotless ---
spotless {
  java {
    googleJavaFormat(libs.versions.googleJavaFormat.get())
    removeUnusedImports()
    importOrder('java','javax','org.springframework','com.quezap','#','',)
    formatAnnotations()
    target('**/*.java')
    targetExclude('**/build/**', '**/generated/**')
  }
  groovyGradle {
    target('**/*.gradle')
    targetExclude('**/build/**')
    leadingTabsToSpaces(2)
  }
}

tasks.named('check') { dependsOn('spotlessCheck') }

// --- JaCoCo ---
jacoco { toolVersion = libs.versions.jacoco.get() }

